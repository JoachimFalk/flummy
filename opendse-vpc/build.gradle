// vim: set syntax=groovy sts=4 ts=8 et:

plugins {
//  id 'de.jfalk.gradle.utilities'
    id 'java-library'
}

// Here, ``this'' is the current org.gradle.api.Project
//this.buildDir = 'obj-gradle'

import java.util.regex.Matcher

class JavaWrapXSDandDTDFiles extends DefaultTask {
    String                          inPackage;
    @InputFiles      FileCollection srcXSDandDTDFiles
    @OutputDirectory File           sourceDir

    @TaskAction
    void action() {
        srcXSDandDTDFiles.files.each { File f ->
            def m = f.name =~ /^(.*)\.(xsd|dtd)$/
            assert m instanceof Matcher
            assert m
            String className = m.group(1).capitalize()+m.group(2).toUpperCase();
            //    println m.group(1) + ":" + m.group(2)
            File output = new File(sourceDir, className+".java");
            output.text = "package " + inPackage + ";\n\n" +
                "class "+className+" {\n" +
                "    public static final String text = \"\"\n";
            f.eachLine { line ->
                String quotedLine = line.replaceAll(/["\\]/) { c -> '\\'+c }
                output.append("      + \""+quotedLine+"\\n\"\n")
            }
            output.append("    ;\n}\n");
        }
    }
}

if (javaBuild) {

compileJava {
    options.debug = true
}

dependencies {
//  analysis "dependencies", it
    if ((rootProject.subprojects.findAll { it.name == "opendse" }).isEmpty()) {
        implementation 'com.github.FedorSmirnov89.opendse:build:7c8ff373b9'
    } else {
        implementation project(':opendse')
    }
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    implementation group: 'org.jdom', name: 'jdom', version: '1.1.3'
}

task javaWrapVPCDTD(type: JavaWrapXSDandDTDFiles) {
    description       = "Wrap vpc.dtd into java file for consumption by the VPCEvaluator."
    inPackage         = "de.fau.scd.VPC.io"
    srcXSDandDTDFiles = parent.project.files("vpc.dtd")
    sourceDir         = project.file("${buildDir}/src/main/java/de/fau/scd/VPC/io")
}

task javaWrapSNGXSD(type: JavaWrapXSDandDTDFiles) {
    description       = "Wrap sng.xsd into java file for consumption by the SNGReader."
    inPackage         = "de.fau.scd.VPC.io"
    srcXSDandDTDFiles = files("src/main/resources/sng.xsd")
    sourceDir         = project.file("${buildDir}/src/main/java/de/fau/scd/VPC/io")
}

sourceSets {
    main {
//      analysis("main", it)
        java {
//          analysis("main.java", it)
            srcDir "src/main/java"
            srcDir "${buildDir}/src/main/java"
        }
    }
}

compileJava.dependsOn javaWrapVPCDTD
compileJava.dependsOn javaWrapSNGXSD

}
